config { 
    type: "operations",
    hasOutput: true
}

CREATE OR REPLACE FUNCTION ${self()} (webhookDisplayNames ARRAY<JSON>,webhookStatuses ARRAY<JSON>,webhookPayloads ARRAY<JSON>,webhookLatencies ARRAY<JSON>)
RETURNS ARRAY<STRUCT<name STRING, status_code STRING, payload JSON, latency_ms INT64>>
LANGUAGE js
AS r"""

  const length = webhookDisplayNames.length;

  if (
    webhookStatuses.length !== length ||
    webhookPayloads.length !== length ||
    webhookLatencies.length !== length
  ) {
    throw new Error('Input arrays must have the same length.');
  }

  const combinedArray = [];

  for (let i = 0; i < length; i++) {
    combinedArray.push({
      name: webhookDisplayNames[i],
      status_code: webhookStatuses[i].code,
      payload: webhookPayloads[i],
      latency_ms: Number(webhookLatencies[i].nanos) / 1000000
    });
  }

  return combinedArray;

""";